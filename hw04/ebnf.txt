xml = element ;
element = { whitespace } tags { whitespace } ;
tags = empty_tag | start_tag { whitespace } content { whitespace } end_tag ;
empty_tag = '<' { whitespace } name { attributes } '/>' ;
start_tag = '<' { whitespace } name { attributes } '>' ;
end_tag = '</' { whitespace } name { whitespace } '>' ;
content = text | element { element } ;
attributes = { whitespace attribute } { whitespace } ;
attribute = name equals value ;
equals = { whitespace } '=' { whitespace };
value = '"' text '"' ;
text = { reference | ? any character except '<' '&' and '"' ? } ;
name = name_init { name_init | digit | '.' | '-' | ':' } ;
name_init = letter | '_' ;
digit = ? isdigit() ? ;
hex_digit = ? isxdigit() ? ;
reference = char_ref | entity_ref ;
letter = ? isalpha() ? ;
char_ref = digit_ref | hexdigit_ref ;
digit_ref = '&#' digit { digit } ';' ;
hexdigit_ref = '&#x' hexdigit { hexdigit } ';' ;
entity_ref = '&' ( 'gt' | 'lt' | 'amp' | 'quot' | 'apos' ) ';' ;
whitespace = { ? isspace() ? } ;
